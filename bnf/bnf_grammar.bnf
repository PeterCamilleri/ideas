# A BNF Grammar for BNF Compiler Specifications.

begin BNF_Grammar

pragma version "0.0.9"

<grammar>      = <blank>? <begin> <statement>+ <end>

<begin>        = "begin" <class_name> <action>? <separator>

<class_name>   = /\A[A-Z][A-Za-z0-9_]*/

<end>          = "end" <action>?

<statement>    = (<rule>|<pragma>) <separator>

<rule>         = <rule_name> "=" <expression>

<rule_name>    = /\A\<[a-z][a-z0-9_]*\>/

<expression>   = <term> ("|" term)*

<term>         = <factor>+

<factor>       = <value> ("?"|"*"|"+")? <action>?

<value>        = <string> | <regex> | <rule_name> | <sub_expr>

<string>       = /\A"([^=]|(\"))*"/

<regex>        = /\A(\/)((\\\/)|[^\/])*(\/)[imx]*/

<sub_expr>     = "(" <expression> ")"

<separator>    = ";" | <blank>

<blank>        = /\A\s*$(^\s*$)+/

<action>       = /\A{{.*}}/m

<pragma>       = "pragma" <selector> <string>

<selector>     = "version"

<spaces>       = /\A\s+/  {{ false }}

<comments>     = /\A#.*$/ {{ false }}

end
